\chapter{Построение и генерация сеток} 

Первым шагом при численном решении уравнений является покрытие области задачи вычислительной сеткой. При дискретизации области встает вопрос о зависимости величины ячеек разбиения области и точности решения полученной с применением определенной сетки. Для большинства задач предполагается, что уменьшение размера ячеек сетки приводит к увеличению точности решения задачи. Но это справедливо не для всех задач. Существуют задачи с сингулярностью, для которых решение задачи может не сходиться к точному решению при уменьшении локального размера сетки, а в некоторых случаях даже расходиться. В большинстве случаев это связано с выбором некорректной модели, геометрии или численного метода не приспособленного под особенность задачи.

Отметим также, что для задач, которые сходятся к точному решению при измельчении сетки, не обязательно уменьшать размер сетки на всей области. Если решение задачи имеет участки области, где наблюдается наибольшее отклонение относительно точного решения, то при измельчении сетки только в них мы получим сопоставимое по точности решение задачи при гораздо меньшей нагрузке ЭВМ по сравнению с решением, полученном при равномерном измельчении сетки на всей области. Таким образом, при построении дискретизации уравнений полезно иметь представление о локальных особенностях задачи. 


\section{Программа Gmsh}
На своей многолетней практике вычислений мне довелось работать с двумя генераторами сетки в виду их хорошей совместимостью с вычислительным пакетом \texttt{FEniCS}\footnote{\texttt{https://fenicsproject.org/}}. Эти программы \texttt{Netgen}\footnote{\texttt{https://ngsolve.org/}} и \texttt{Gmsh}\footnote{\texttt{http://gmsh.info/}}. В силу универсальности, хорошей поддержки и большой популярности \texttt{Gmsh} вне конкуренции. 

\subsection*{Альтернативные программы}
Для создания геометрии и генерации сетки можно воспользоваться встроенным редактором геометрии \texttt{FEniCS}, который может быть внедрен в основной код. Однако при работе с областью, имеющей сложную форму и геометрию, рекомендуется импортировать сетку из специализированных программ по генерации вычислительных сеток, таких как \texttt{Gmsh} и \texttt{Netgen}, которые также являются свободно распространяемыми. 

Отличительной особенностью \texttt{Gmsh} является работа с примитивами, такими как точки, линии, поверхности и объемы. Подход Netgen основан на булевых операциях над геометрическими объектами, таких как параллелепипед, шар, эллипсоид, полуплоскости и т. д. 

До недавних времен в зависимости от моделируемой геометрии удобнее было пользоваться либо \texttt{Gmsh}, либо \texttt{Netgen}. Сегодня булевы операции доступны и \texttt{Gmsh} при подключении ядра \texttt{OpenCASCADE}\footnote{\texttt{https://www.opencascade.com/}}. В связи с этим, овладев \texttt{Gmsh}, можно строить сетки любой сложности.

\section{Процесс создания геометрии}
Опишем процесс создания сетки на примере создания геометрии куба с центральным вырезом, заполненным другим материалом, который имеет форму круга с одного конца, и форму эллипса с другого. Схематично геометрия выглядит как на рис. \ref{figgmsh:geom}.
\begin{figure}[h]
  \begin{center}
  \begin{minipage}{0.3\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p1.png}
  \end{minipage}
  \begin{minipage}{0.3\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p2.png}
  \end{minipage}
  \begin{minipage}{0.3\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p3.png}
  \end{minipage}    
	\caption{Пример геометрии}
	\label{figgmsh:geom}
  \end{center}
\end{figure}
\subsection*{Создание точек}
Для начала зададим переменные для наших параметры: длину стороны куба, радиус кругового выреза, длину большой и малой полуосей эллиптического выреза и параметр локального размера элементов сетки.
\begin{lstlisting}
l = 1;
r = 0.2;
ls = 0.05;
a = 0.3;
b = 0.15;
\end{lstlisting}
Синтаксис очень простой -- после названия переменной следует символ присваивания и значение переменной. Также важно помнить о точке с запятой в конце каждой строчки, как языке программирования C.

Конечно, можно обойтись без параметров, и писать все значения вручную, но не стоит пренебрегать заданием переменных. Ведь в случае, когда условие задачи поменяется или нужно будет строить схожую сетку с другими размерами, гораздо удобнее менять одно значение переменной, чем менять его везде где он был использован.
\subsection*{Создание точек}
После определения переменных можно приступать к созданию точек нашей сетки: это соответственно вершины куба, центры круга и эллипса, а также точки на круге и эллипсе.
\begin{lstlisting}
Point(1) = {0, 0, 0, ls};
Point(2) = {l, 0, 0, ls};
Point(3) = {l, l, 0, ls};
Point(4) = {0, l, 0, ls};

Point(5) = {l/2, l/2, 0, ls};
Point(6) = {l/2+r, l/2, 0, ls};
Point(7) = {l/2, l/2+r, 0, ls};
Point(8) = {l/2-r, l/2, 0, ls};
Point(9) = {l/2, l/2-r, 0, ls};

Point(11) = {0, 0, l, ls};
Point(12) = {l, 0, l, ls};
Point(13) = {l, l, l, ls};
Point(14) = {0, l, l, ls};

Point(15) = {l/2, l/2, l, ls};
Point(16) = {l/2+a, l/2, l, ls};
Point(17) = {l/2, l/2+b, l, ls};
Point(18) = {l/2-a, l/2, l, ls};
Point(19) = {l/2, l/2-b, l, ls};
\end{lstlisting}
Отметим сразу удобство использования переменных для задания координат наших точек. Синтаксис задания точек следующий:
\begin{lstlisting}
Point(*номер точки*) = {*значение координаты x*, 
*значение координаты y*, *значение координаты z*, 
*локальная величина размеров ячейки сетки*}
\end{lstlisting}
В итоге получим распределение точек как представлено на рис. \ref{figgmsh:dots}.
\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.8\textwidth]{gmsh/p4.png}
	\caption{Геометрия на этапе создания точек}
	\label{figgmsh:dots}
  \end{center}
\end{figure}
Пока ничего толком не понятно, что мы нарисовали. 
\subsection*{Рисование линий и кривых}
Далее нам нужно соединить между собой соответствующие точки: линии граней куба, круговые и эллиптические кривые и линии, соединяющие круг с эллипсом:
\begin{lstlisting}
Line(1) = {1, 2};
Line(2) = {2, 3};
Line(3) = {3, 4};
Line(4) = {4, 1};

Circle(5) = {6, 5, 7};
Circle(6) = {7, 5, 8};
Circle(7) = {8, 5, 9};
Circle(8) = {9, 5, 6};

Line(11) = {11, 12};
Line(12) = {12, 13};
Line(13) = {13, 14};
Line(14) = {14, 11};

Ellipse(15) = {16, 15, 16, 17};
Ellipse(16) = {17, 15, 18, 18};
Ellipse(17) = {18, 15, 18, 19};
Ellipse(18) = {19, 15, 16, 16};

Line(21) = {1, 11};
Line(22) = {2, 12};
Line(23) = {3, 13};
Line(24) = {4, 14};
Line(25) = {6, 16};
Line(26) = {7, 17};
Line(27) = {8, 18};
Line(28) = {9, 19};
\end{lstlisting}
Синтаксис создания линий следующий:
\begin{lstlisting}
Line(*номер линии*) = {*номер точки начала линии*, 
                       *номер точки конца линии*}
\end{lstlisting}
Заметим, что очередность начала и конца точки имеет значение (подробнее при создании поверхности).
Для создания круга и эллипса параметров немного больше. И тут становится понятно -- зачем нужны точки по центру.
\begin{lstlisting}
Circle(*номер кривой*) = {*номер точки начала кривой*, 
                          *номер точки центра кривой*, 
                          *номер точки конца кривой*}
Ellipse(*номер кривой*) = {*номер точки начала кривой*, 
                           *номер точки центра кривой*, 
                           *номер точки на большой полуоси эллипса*,               
                           *номер точки конца кривой*}
\end{lstlisting}
Кривые Circle и Ellipse представляют собой часть фигуры, который занимает сектор в 90 градусов или четверть окружности или эллипса, соответственно.
В итоге добавления вышеописанных строк мы получим вид представленный на рис. \ref{figgmsh:lines}.
\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.8\textwidth]{gmsh/p5.png}
	\caption{Геометрия на этапе создания линий}
	\label{figgmsh:lines}
  \end{center}
\end{figure}

\subsection*{Объединение кривых в поверхности}
Для создания поверхностей необходимо соединить линии в замкнутые кривые, которые являются границами поверхностей. Используя номера кривых, составляющих внешние грани и грани внутренних вырезов, мы создаем поверхности.
\begin{lstlisting}
Line Loop(1) = {1, 2, 3, 4};
Line Loop(2) = {5, 6, 7, 8};
Plane Surface(1) = {1, 2};
Plane Surface(2) = {2};
\end{lstlisting}
Первая замкнутая кривая включает номера линий границы нижнего квадрата, вторая -- границы круга. Первая поверхность в качестве параметров имеет две кривые, потому что имеет вырез по середине. 
\begin{lstlisting}
Line Loop(3) = {11, 12, 13, 14};
Line Loop(4) = {15, 16, 17, 18};
Plane Surface(3) = {3, 4};
Plane Surface(4) = {4};
\end{lstlisting}
То же самое выполняется для верхней грани тела. В итоге мы получаем поверхности, представленные на рис. \ref{figgmsh:surf1}.
\begin{figure}[h]
  \begin{center}
  \begin{minipage}{0.45\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p6.png}
  \end{minipage}
  \begin{minipage}{0.45\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p7.png}
  \end{minipage} 
	\caption{Геометрия на этапе создания поверхностей}
	\label{figgmsh:surf1}
  \end{center}
\end{figure}
\begin{lstlisting}
Line Loop(5) = {1, 22, -11, -21};
Plane Surface(5) = {5};
Line Loop(6) = {2, 23, -12, -22};
Plane Surface(6) = {6};
Line Loop(7) = {3, 24, -13, -23};
Plane Surface(7) = {7};
Line Loop(8) = {4, 21, -14, -24};
Plane Surface(8) = {8};
\end{lstlisting}
Строки описывают поверхности по внешним граням куба. Эти грани показаны на рис. \ref{figgmsh:surf2}. Обратите внимание на то, что нумерация линий содержит отрицательные числа. Это обуславливается тем, что отрицательные значения присваиваются линиям, имеющим направление противоположное направлению обхода (пример рис. \ref{figgmsh:lloop}).
\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.5\textwidth]{gmsh/p18.png}
	\caption{Обход линий для создания Line Loop}
	\label{figgmsh:lloop}
  \end{center}
\end{figure}
\begin{lstlisting}
Line Loop(9) = {5, 26, -15, -25};
Ruled Surface(9) = {9};
Line Loop(10) = {6, 27, -16, -26};
Ruled Surface(10) = {10};
Line Loop(11) = {7, 28, -17, -27};
Ruled Surface(11) = {11};
Line Loop(12) = {8, 25, -18, -28};
Ruled Surface(12) = {12};
\end{lstlisting}
Для создания не плоских поверхностей используется функция Ruled Surface. Полученные поверхности представлены на рис. \ref{figgmsh:surf2}.
\begin{figure}[h]
  \begin{center}
  \begin{minipage}{0.45\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p8.png}
  \end{minipage}
  \begin{minipage}{0.45\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p9.png}
  \end{minipage} 
	\caption{Геометрия на этапе создания поверхностей}
	\label{figgmsh:surf2}
  \end{center}
\end{figure}

\subsection*{Создание объема}
Для создания объема необходимо собрать список поверхностей, составляющих границы объема.
\begin{lstlisting}
Surface Loop(1) = {1, 3, 5, 6, 7, 8, 9, 10, 11, 12};
Volume(1) = {1};
Surface Loop(2) = {2, 4, 9, 10, 11, 12};
Volume(2) = {2};
\end{lstlisting}
В итоге получим необходимые нами объемы представленные на рис. \ref{figgmsh:geom}. 

\subsection*{Определение физических групп}
Для связи нумерации границ и подобластей необходимо определить физические группы. Именно номера физических групп будут использоваться в вычислениях. 
\begin{lstlisting}
Physical Surface(1) = {1, 3};
Physical Surface(2) = {2, 4};
Physical Surface(3) = {5, 6, 7, 8};

Physical Volume(1) = {2};
Physical Volume(2) = {1};
\end{lstlisting}

Использование отдельной нумерации физических групп позволяет объединять несколько поверхностей или объемов под одним номером. Полученные физические группы подобластей и границ, представлены на рис. \ref{figgmsh:phvol}, \ref{figgmsh:dots}, соответственно.

\begin{figure}[h]
  \begin{center}
  \begin{minipage}{0.45\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p13.png}
  \end{minipage}
  \begin{minipage}{0.45\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p14.png}
  \end{minipage} 
	\caption{Физические группы подобластей}
	\label{figgmsh:phvol}
  \end{center}
\end{figure}

\begin{figure}[h]
  \begin{center}
  \begin{minipage}{0.3\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p15.png}
  \end{minipage}
  \begin{minipage}{0.3\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p16.png}
  \end{minipage} 
  \begin{minipage}{0.3\textwidth}
  \includegraphics[width = \textwidth]{gmsh/p17.png}
  \end{minipage} 
	\caption{Физические группы границ}
	\label{figgmsh:phsurf}
  \end{center}
\end{figure}

\section{Создание сетки}
Для создания вычислительной сетки откройте файл внутри \texttt{Gmsh}, откройте вкладку \texttt{Mesh} и нажмите на \texttt{3D}. Должно получится что-то вроде сетки, представленной на рис. \ref{figgmsh:vol}.
\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.8\textwidth]{gmsh/p12.png}
	\caption{Вычислительная сетка}
	\label{figgmsh:vol}
  \end{center}
\end{figure}

\subsection*{Виды генераторов сетки}
Если вы откроете настройки \texttt{Options} и вкладку \texttt{Mesh}, то вы заметите множество алгоритмов для создания 2D и 3D сеток. Выбор зависит от вас. От себя рекомендую алгоритм \texttt{MeshAdapt} для 2D и \texttt{Frontal} для 3D. Иногда один лишь правильный выбор алгоритмов генерации сеток позволяет значительно ускорить решение краевой задачи. Пробуйте и сравнивайте различные алгоритмы. 

\subsection*{Структурированные сетки}
Не могу припомнить случай когда возникала необходимость в создании структурированных сеток для решения практических задач. Но для тех, кто интересуется, представлю код по созданию структурированной сетки на квадратной области.

\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.8\textwidth]{gmsh/2_2.png}
	\caption{Структурированная сетка}
	\label{figgmsh:struct}
  \end{center}
\end{figure}

Для создания структурированной сетки, представленной на рис. \ref{figgmsh:struct}, необходимо написать следующий код
\begin{lstlisting}
l = 10;
ls = 0.1;

Point(1) = {0, 0, 0, ls};
Point(2) = {l, 0, 0, ls};
Point(3) = {l, l, 0, ls};
Point(4) = {0, l, 0, ls};

Line(1) = {1, 2};
Line(2) = {2, 3};
Line(3) = {3, 4};
Line(4) = {4, 1};

Line Loop(1) = {1, 2, 3, 4};

Plane Surface(1) = {1};
Transfinite Curve {1:4} = 10;
Transfinite Surface{1};
Physical Surface(1) = {1};
\end{lstlisting}
Здесь число после \texttt{Transfinite Curve {1:4}} означает количество точек в линиях от 1 до 4, равное 10.

\clearpage
\section{Другие возможности Gmsh}
Отметим другие возможности по построению сеток на \texttt{Gmsh}. Для этого рассмотрим пример вычислительной сетки представленной на рис. \ref{figgmsh:func}.
\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.7\textwidth]{gmsh/2_1.png}
	\caption{Пример вычислительной сетки. Обозначение цветов: серый -- физическая область №1, зеленый -- №2, синий -- №3, красный -- №4}
	\label{figgmsh:func}
  \end{center}
\end{figure}

Полный код файла сетки выглядит как
\begin{lstlisting}
l = 10;
ls = 0.1;
r1 = 0.3;
r2 = 0.2;
X = 0;
Y = 0;
R = 0;

Point(1) = {0, 0, 0, ls};
Point(2) = {l, 0, 0, ls};
Point(3) = {l, l, 0, ls};
Point(4) = {0, l, 0, ls};

Line(1) = {1, 2};
Line(2) = {2, 3};
Line(3) = {3, 4};
Line(4) = {4, 1};

Line Loop(1) = {1, 2, 3, 4};

loops[] = {1};
For j In {0:2}
  surf~{j}[] = {};
EndFor

Function CircleLoop
  cp1 = newp; Point(cp1) = {X, Y, 0, ls};
  cp2 = newp; Point(cp2) = {X+R, Y, 0, ls};
  cp3 = newp; Point(cp3) = {X, Y+R, 0, ls};
  cp4 = newp; Point(cp4) = {X-R, Y, 0, ls};
  cp5 = newp; Point(cp5) = {X, Y-R, 0, ls};
  cl1 = newl; Circle(cl1) = {cp2, cp1, cp3};
  cl2 = newl; Circle(cl2) = {cp3, cp1, cp4};
  cl3 = newl; Circle(cl3) = {cp4, cp1, cp5};
  cl4 = newl; Circle(cl4) = {cp5, cp1, cp2};
  cll = news; Line Loop(cll) = {cl1, cl2, cl3, cl4};
  loops[] += {cll};
  If((i+j+3)%11 < 3)
    Plane Surface(cll) = {cll};
    surf~{(i+j+3)%11}[] += {cll};
  EndIf
Return

For i In {0:9}
  For j In {0:9}
   X = 0.5 + i;
   Y = 0.5 + j;
   If(i%2==0)
     R = r1;
   Else
     R = r2;
   EndIf
   Call CircleLoop;
  EndFor
EndFor

Plane Surface(1) = {loops[]};
Physical Surface(1) = {1};
For j In {0:2}
  Physical Surface(j+2) = {surf~{j}[]};
EndFor
\end{lstlisting}

Здесь используются возможности \texttt{Gmsh} по созданию и вызову функций, использования циклов и условных операторов. Рассмотрим их подробнее.

\subsection*{Функции} 
Создание функции сопровождается вызовом команды \texttt{Function}
\begin{lstlisting}
Function CircleLoop
\end{lstlisting}
После вызова этой команды начинается наполнение тела создаваемой функции, которое заканчивается вызовом команды \texttt{Return}
\begin{lstlisting}
Return
\end{lstlisting}
Отметим, что в представленном выше коде в теле функции присутствуют схожие команды \texttt{newp}, \texttt{newl} и \texttt{news}. Эти команды используются для получения нового номера для точки, линии и поверхности, соответственно. Для получения нового не использованного номера для областей вызывается команда \texttt{newv}. 

Эти команды помогают пользователю не использовать многократно один и тот же номер для обозначения разных объектов. Получаемый номер не какой-то особенный. Как правило он на единицу выше максимального существующего номера, который имеет объект этого же типа (уровня = точка, линия, поверхность, объем).

Для вызова функции необходимо следовать синтаксису \texttt{Call} + название функции. В нашем случае
\begin{lstlisting}
Call CircleLoop;
\end{lstlisting}

\subsection*{Циклы, условные операторы}
Циклы и условные операторы известны и понятны всем, кто хоть немного знает о программировании. В \texttt{Gmsh} ничего нового в этом плане нет.

Синтаксис для циклов выглядит как
\begin{lstlisting}
For переменная In {начальное значение:конечное значение}
  Тело цикла
EndFor
\end{lstlisting}
а условного оператора так
\begin{lstlisting}
   If(Условие)
     Тело условного оператора при выполнении условия
   Else
     Тело условного оператора при не выполнении условия
   EndIf
\end{lstlisting}
Отметим, что при вызове циклов бывает полезно использовать пополняемые списки. Имеются ввиду следующие строчки кода
\begin{lstlisting}
loops[] = {1};
For j In {0:2}
  surf~{j}[] = {};
EndFor

...

loops[] += {cll};
surf~{(i+j+3)%11}[] += {cll};

...

Plane Surface(1) = {loops[]};
Physical Surface(1) = {1};
For j In {0:2}
  Physical Surface(j+2) = {surf~{j}[]};
EndFor
\end{lstlisting}
в которых показано их создание, пополнение и использование. 

Здесь цикл
\begin{lstlisting}
For j In {0:2}
  surf~{j}[] = {};
EndFor
\end{lstlisting}
заменяет запись
\begin{lstlisting}
surf_0[] = {};
surf_1[] = {};
surf_2[] = {};
\end{lstlisting}

\newpage
\subsection*{Ядро OpenCASCADE}
C помощью ядра \texttt{OpenCASCADE} получилось за 17 строчек кода создать геометрию симпатичного робота представленного на рис. \ref{figgmsh:robo}.
\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.8\textwidth]{gmsh/2_3.png}
	\caption{Пример вычислительной сетки созданной с помощью ядра OpenCASCADE}
	\label{figgmsh:robo}
  \end{center}
\end{figure}

Соответствующий код представлен далее
\begin{lstlisting}
SetFactory("OpenCASCADE");

Block(1) = {0, 0, 0, 2, 2, 1};
Cylinder(2) = {0.5, 1.5, -0.5, 0, 0, 2, 0.25, 2*Pi};
Cylinder(3) = {1.5, 1.5, -0.5, 0, 0, 2, 0.25, 2*Pi};
Cylinder(4) = {1.0, 0.8, 0.5, 0, 0, 1.0, 0.25, 2*Pi};

BooleanDifference{ Volume{1}; Delete; }{ Volume{2}; Volume{3}; Delete; }
BooleanUnion{ Volume{1}; Delete; }{ Volume{4}; Delete; }

radius = DefineNumber[ 0.05, Name "Parameters/radius" ];
Fillet{1}{11, 12, 19}{radius}

Block(5) = {-1, -4, 0, 2, 4, 1};
Block(6) = {-3, -1, 0, 2, 1, 1};
Block(7) = {-3, -3, 0, 1, 2, 1};
Block(8) = {-1, -8, 0, 1, 4, 1};
Block(9) = {-1, -8, 1, 1, 1, 1};

BooleanUnion{ Volume{5}; Delete; }{ Volume{6}; Volume{7}; 
                          Volume{8}; Volume{9}; Delete; }
Symmetry {1, 0, 0, -1} {Duplicata { Volume{2}; }}

BooleanUnion{ Volume{1}; Delete; }{ Volume{2}; Volume{3}; Delete; }
\end{lstlisting}

Если рассмотреть подробнее код сетки, то сначала было инициализировано ядро \texttt{OpenCASCADE}, которое позволяет производить булевы операции сложения и вычитания. После чего были созданы объекты, которые друг от друга вычитались и объединялись.

Отметим, что с помощью функции \texttt{Fillet} можно скруглить элемент объема возле включенных в список линий. 

Для дальнейшего ознакомления прочитайте официальную документацию \texttt{Gmsh}. Там описаны все используемые функции и имеется множество полезных примеров.

\clearpage
\section*{Задания}
\begin{itemize}
\item[1.] Постройте вычислительные сетки для следующих двумерных объектов: круг, квадрат, гексагон. 
\item[2.] Постройте вычислительные сетки для следующих трехмерных объектов: сфера, куб, конус.
\item[3.] Постройте вычислительную сетку для двумерного объекта c ромбовидными перфорациями, представленного на рис. \ref{figgmsh:prob2d}.
\begin{figure}[h]
  \begin{center}
  \includegraphics[width = 0.8\textwidth]{gmsh/prob.png}
	\caption{Задание}
	\label{figgmsh:prob2d}
  \end{center}
\end{figure}
\item[4.] Постройте вычислительную геометрию стола с четырьмя ножками, используя ядро OpenCASCADE.
\end{itemize}